plugins {
    id 'java-library'
}

base {
    archivesName = "${mod_id}-${project.name}-${minecraft_version}"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
}

repositories {
    mavenCentral()
    exclusiveContent {
        forRepository {
            maven {
                name = 'Sponge'
                url = 'https://repo.spongepowered.org/repository/maven-public'
            }
        }
        filter { includeGroupAndSubgroups('org.spongepowered') }
    }
    exclusiveContent {
        forRepositories(
                maven {
                    name = 'ParchmentMC'
                    url = 'https://maven.parchmentmc.org/'
                },
                maven {
                    name = "NeoForge"
                    url = 'https://maven.neoforged.net/releases'
                }
        )
        filter { includeGroup('org.parchmentmc.data') }
    }
    maven {
        name = 'CurseMaven'
        url = 'https://cursemaven.com'
    }
    maven {
        name = 'BlameJared'
        url = 'https://maven.blamejared.com'
    }
    maven {
        name = 'Architectury'
        url = 'https://maven.architectury.dev/'
        content {
            includeGroup 'dev.architectury'
            includeGroup 'me.shedaniel'
            includeGroup 'me.shedaniel.cloth'
        }
    }
    maven {
        name = 'FTB'
        url = 'https://maven.saps.dev/releases/'
    }
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
}

// Declare capabilities on the outgoing configurations.
['apiElements', 'runtimeElements', 'sourcesElements'].each { variant ->
    configurations."$variant".outgoing {
        capability("$group:${base.archivesName.get()}:$version")
        capability("$group:$mod_id-${project.name}-${minecraft_version}:$version")
        capability("$group:$mod_id:$version")
    }
}

jar {
    manifest {
        attributes([
                'Specification-Title'   : mod_name,
                'Specification-Vendor'  : mod_authors,
                'Specification-Version' : project.jar.archiveVersion,
                'Implementation-Title'  : project.name,
                'Implementation-Version': project.jar.archiveVersion,
                'Implementation-Vendor' : mod_authors,
                'Built-On-Minecraft'    : minecraft_version
        ])
    }
}

processResources {
    var expandProps = [
            'version'                : version,
            'group'                  : project.group,
            'minecraft_version'      : minecraft_version,
            'minecraft_version_range': minecraft_version_range,
            'fabric_version'         : fabric_version,
            'fabric_loader_version'  : fabric_loader_version,
            'mod_name'               : mod_name,
            'mod_authors'            : mod_authors,
            'mod_id'                 : mod_id,
            'mod_license'            : mod_license,
            'mod_description'        : mod_description,
            'neoforge_version'       : neoforge_version,
            'neo_version_range'      : neo_version_range,
            'loader_version_range'   : loader_version_range,
            'forge_version'          : forge_version,
            'forge_loader_version_range' : forge_loader_version_range,
    ]

    filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', 'META-INF/neoforge.mods.toml', '*.mixins.json']) {
        expand expandProps
    }
    inputs.properties(expandProps)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
