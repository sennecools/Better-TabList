plugins {
    id 'dev.kikugie.stonecutter'
    id 'fabric-loom' version '1.15.3'
    id 'me.modmuss50.mod-publish-plugin'
    id 'java-library'
}

stonecutter {
    def loader = 'fabric'
    constants['fabric'] = true
    constants['neoforge'] = false
}

base {
    archivesName = "${mod_id}-fabric-${stonecutter.current.version}"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org/'
    }
    maven {
        name = 'CurseMaven'
        url = 'https://cursemaven.com'
    }
    maven {
        name = 'Architectury'
        url = 'https://maven.architectury.dev/'
    }
    maven {
        name = 'FTB'
        url = 'https://maven.saps.dev/releases/'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    include implementation("com.electronwill.night-config:toml:${nightconfig_version}")
    include implementation("com.electronwill.night-config:core:${nightconfig_version}")

    compileOnly "dev.architectury:architectury-neoforge:${architectury_api_version}"
    compileOnly "curse.maven:ftb-library-forge-404465:6711324"
    compileOnly "curse.maven:ftb-ranks-forge-314905:6431744"
}

loom {
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(stonecutter.current.isActive)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(stonecutter.current.isActive)
            runDir('runs/server')
        }
    }
}

sourceSets.main.resources.srcDir rootProject.file('src/main/resources-fabric')

processResources {
    var expandProps = [
            'version'                : version,
            'group'                  : project.group,
            'minecraft_version'      : minecraft_version,
            'minecraft_version_range': minecraft_version_range,
            'fabric_version'         : fabric_version,
            'fabric_loader_version'  : fabric_loader_version,
            'mod_name'               : mod_name,
            'mod_authors'            : mod_authors,
            'mod_id'                 : mod_id,
            'mod_license'            : mod_license,
            'mod_description'        : mod_description,
    ]

    filesMatching(['fabric.mod.json', '*.mixins.json']) {
        expand expandProps
    }
    inputs.properties(expandProps)

    // Exclude NeoForge-specific resources
    exclude 'META-INF/neoforge.mods.toml'
}

jar {
    manifest {
        attributes([
                'Specification-Title'   : mod_name,
                'Specification-Vendor'  : mod_authors,
                'Specification-Version' : project.jar.archiveVersion,
                'Implementation-Title'  : project.name,
                'Implementation-Version': project.jar.archiveVersion,
                'Implementation-Vendor' : mod_authors,
                'Built-On-Minecraft'    : minecraft_version,
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

publishMods {
    file = remapJar.archiveFile
    changelog = rootProject.file("CHANGELOG.md").exists() ? rootProject.file("CHANGELOG.md").text : "Release ${project.version}"
    type = STABLE
    modLoaders.add("fabric")
    displayName = "${mod_name} ${project.version} for ${minecraft_version} [Fabric]"

    def localProps = new Properties()
    def localFile = rootProject.file("local.properties")
    if (localFile.exists()) localProps.load(localFile.newReader())
    def modrinthToken = providers.environmentVariable("MODRINTH_TOKEN").getOrNull() ?: localProps.getProperty("MODRINTH_TOKEN")

    dryRun = modrinthToken == null

    modrinth {
        projectId = "3o3inmWM"
        accessToken = modrinthToken
        minecraftVersions.add(minecraft_version)
        requires("fabric-api")
    }
}
